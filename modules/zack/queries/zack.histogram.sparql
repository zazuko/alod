PREFIX : <http://voc.zazuko.com/zack#>
PREFIX  rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX  text: <http://jena.apache.org/text#>
PREFIX  rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX  skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX  xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX time: <http://www.w3.org/2006/time#>

SELECT (COUNT(?sub) as ?histo) ?bucket ?bucket_start ?bucket_end
WHERE {
  GRAPH <http://data.alod.ch/graph/inference-supertitle> {
    ?sub text:query (skos:hiddenLabel "${searchString}") .
  }
  GRAPH ?g {
    ?sub time:intervalStarts ?start_ .
    FILTER( datatype(xsd:date(?start_)) = xsd:date || datatype(xsd:date(CONCAT(?start_,"-01-01"))) = xsd:date)
    BIND ( IF(STRLEN(xsd:string(?start_)) = 4, xsd:date(CONCAT(?start_,"-01-01")), xsd:date(?start_)) AS ?start )

    ${filters}
  }

  {
    SELECT ( MIN(?_start) as ?min ) ( COUNT(?sub) as ?count ) ( ( day( MAX(?_start) - MIN(?_start) ) / ${width} ) as ?interval )
    WHERE {
      GRAPH <http://data.alod.ch/graph/inference-supertitle> {
        ?sub text:query (skos:hiddenLabel "${searchString}") .
      }
      GRAPH ?g {
        ?sub time:intervalStarts ?_start_ .
        FILTER( datatype(xsd:date(?_start_)) = xsd:date || datatype(xsd:date(CONCAT(?_start_,"-01-01"))) = xsd:date)
        BIND ( IF(STRLEN(xsd:string(?_start_)) = 4, xsd:date(CONCAT(?_start_,"-01-01")), xsd:date(?_start_)) AS ?_start )

        ${filters}
      }
    }
  }

  BIND (floor((day(?start - ?min)/?interval)) AS ?bucket)
  BIND ((?min + xsd:duration(concat('P',xsd:string(floor(floor((day(?start - ?min)/?interval))*?interval)),'D'))) AS ?bucket_start )
  BIND ((?min + xsd:duration(concat('P',xsd:string(floor(floor(((day(?start - ?min)+?interval)/?interval))*?interval)),'D'))) AS ?bucket_end )

}

GROUP BY ?bucket ?bucket_start ?bucket_end
ORDER BY ?bucket
