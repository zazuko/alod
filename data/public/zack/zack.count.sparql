PREFIX : <http://voc.zazuko.com/zack#>
PREFIX  rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX  text: <http://jena.apache.org/text#>
PREFIX  rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX  skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX  xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
  _:b0 :numberOfResults ?count.
  _:b0 :queryStart ?querystart.
  _:b0 :queryEnd ?queryend.
}
WHERE {
  { 
    SELECT (COUNT(?sub) as ?count) {
      GRAPH <http://data.alod.ch/graph/inference-supertitle> {
        (?sub ?score ?literal) text:query (skos:hiddenLabel "${searchString}") .
      }${filters}
    }
  }
  UNION
  {
    SELECT (MIN(?resourcestart) as ?querystart) (MAX(?resourceend) as ?queryend) {
      GRAPH <http://data.alod.ch/graph/inference-supertitle> {
        (?sub ?score ?literal) text:query (skos:hiddenLabel "${searchString}") .
      }${filters}
      GRAPH ?t {
        OPTIONAL {
          ?sub <http://www.w3.org/2006/time#intervalEnds> ?resourceend .
        }
        OPTIONAL {
          ?sub <http://www.w3.org/2006/time#intervalStarts> ?resourcestart .  
        }
        FILTER( datatype(xsd:date(?resourcestart)) = xsd:date)
        FILTER( datatype(xsd:date(?resourceend)) = xsd:date)
      }
    }
  }
}
